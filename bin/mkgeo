#!/usr/bin/perl

use strict;
use warnings;

use IO::File;
use Math::Trig qw(deg2rad pip2 great_circle_distance);
use JSON::XS;
use LWP::UserAgent;
use XML::TreeBuilder;
use YAML;

my $source = shift @ARGV;
my $zone = shift @ARGV;
my $servers = YAML::LoadFile("src/${source}");

# Initialise server details
while (my($name,$server) = each %$servers)
{
    $server->{name} = $name;
    $server->{bandwidth} = $server->{bandwidth} * 1024 * 1024;

    if ($ENV{PINGDOM_USERNAME} && $ENV{PINGDOM_PASSWORD})
    {
        $server->{status} = "down";
    }
    else
    {
        $server->{status} = "up";
    }
}

# If pingdom support is enabled then check which servers are up
if ($ENV{PINGDOM_USERNAME} && $ENV{PINGDOM_PASSWORD})
{
    my $ua = LWP::UserAgent->new;

    $ua->default_header("App-Key", "2cohi62u5haxvqmypk3ljqqrze1jufrh");
    $ua->credentials("api.pingdom.com:443", "Pingdom API", $ENV{PINGDOM_USERNAME}, $ENV{PINGDOM_PASSWORD});

    foreach my $server (values %$servers)
    {
        if (my $checkid = $server->{pingdom})
        {
            my $response = $ua->get("https://api.pingdom.com/api/2.0/checks/${checkid}");

            if ($response->is_success)
            {
                my $check = decode_json($response->content);

                $server->{status} = $check->{check}->{status};
            }
        }
    }
}

my %countries = ();
my @mappings = ();

# Create a parser for the country database
my $countries = XML::TreeBuilder->new;

# Parse the country database
$countries->parsefile("lib/countries.xml");

# Load the per-country bandwidth details
my $bandwidth = YAML::LoadFile("bandwidth/${source}.yml");

# Fill in country table and work out which servers each can use
foreach my $country ($countries->look_down("_tag" => "country"))
{
    my $code = $country->look_down("_tag" => "countryCode")->as_text;
    my $name = $country->look_down("_tag" => "countryName")->as_text;
    my $population = $country->look_down("_tag" => "population")->as_text;
    my $bandwidth = $bandwidth->{$code} || 0;
    my $continent = $country->look_down("_tag" => "continent")->as_text;
    my $west = $country->look_down("_tag" => "west")->as_text;
    my $north = $country->look_down("_tag" => "north")->as_text;
    my $east = $country->look_down("_tag" => "east")->as_text;
    my $south = $country->look_down("_tag" => "south")->as_text;
    my $lat = centre_lat( $south, $north );
    my $lon = centre_lon( $west, $east );

    $countries{$code} = {
        code => $code, name => $name, continent => $continent,
        bandwidth => $bandwidth, lat => $lat, lon => $lon
    };

    foreach my $server (values %$servers)
    {
        my $match = match_country($server, $code, $continent);

        if ($server->{status} eq "up" && $match ne "denied")
        {
            my $priority = $match eq "preferred" ? 20 : 10;
            my $distance = distance($lat, $lon, $server->{lat}, $server->{lon});

            push @mappings, {
                country => $countries{$code}, server => $server,
                priority => $priority, distance => $distance
            };
        }
    }
}

# Discard the parsed country database
$countries->delete;

# Loop over the mappings, trying to assign each country to the
# nearest server, but subject to the bandwidth limits;
foreach my $mapping (sort {  $b->{priority} <=> $a->{priority} || $a->{distance} <=> $b->{distance} } @mappings)
{
    my $country = $mapping->{country};
    my $server = $mapping->{server};

    if ($country->{bandwidth} <= $server->{bandwidth} && !exists($country->{server}))
    {
        $country->{server} = $server;
        $server->{bandwidth} = $server->{bandwidth} - $country->{bandwidth};
    }
}

# Loop over the mappings again, assigning anything that is left
# as best we can, and allowing bandwidth limits to be exeeded
foreach my $mapping (sort {  $b->{priority} <=> $a->{priority} || $a->{distance} <=> $b->{distance} } @mappings)
{
    my $country = $mapping->{country};
    my $server = $mapping->{server};

    $country->{server} = $server unless exists($country->{server});
}

# Create JSON collection object
my @json;

# Open output files
my $zonefile = IO::File->new("> data/${zone}") || die "$!";
my $jsonfile = IO::File->new("> json/${zone}.json") || die "$!";

# Output details for each country
foreach my $country (values %countries)
{
    my $server = $country->{server};
    my $clon = $country->{lon};
    my $clat = $country->{lat};
    my $slon = $server->{lon};
    my $slat = $server->{lat};

    if ($clon > 0 && $slon < 0 && 360 + $slon - $clon < $clon - $slon)
    {
        $slon = $slon + 360;
    }
    elsif ($slon > 0 && $clon < 0 && 360 + $clon - $slon < $slon - $clon)
    {
        $clon = $clon + 360;
    }

    $zonefile->print("C\L$country->{code}\E.${zone}:$server->{name}.${zone}:600\n");

    push @json, {
        type => "Feature",
        geometry => {
            type => "LineString",
            coordinates => [ [ $clon, $clat ], [ $slon, $slat ] ]
        },
        properties => {
            country => $country->{name}
        }
    };
}

# Output default records for IPs that can't be mapped to a country
foreach my $server (grep { $servers->{$_}->{default} } keys %$servers)
{
    $zonefile->print("Cxx.${zone}:${server}.${zone}:600\n");
}

# Output the GeoJSON text
$jsonfile->print(encode_json(\@json));

# Close the output files
$jsonfile->close();
$zonefile->close();

exit 0;

#
# Find the centre value between two latitudes
#
sub centre_lat
{
    my $south = shift;
    my $north = shift;

    return ( $south + $north ) / 2;
}

#
# Find the centre value between two longitudes
#
sub centre_lon
{
    my $west = shift;
    my $east = shift;
    my $lon;

    if ($west < $east)
    {
        $lon = ( $west + $east ) / 2;
    }
    else
    {
        $lon = ( $west + $east + 360 ) / 2;
    }

    $lon = $lon - 360 if $lon > 180;

    return $lon
}

#
# Match a country against a server
#
sub match_country
{
    my $server = shift;
    my $country = shift;
    my $continent = shift;
    my $match;

    if ($server->{preferred} &&
        $server->{preferred}->{countries} &&
        grep { $_ eq $country } @{$server->{preferred}->{countries}})
    {
        $match = "preferred";
    }
    elsif ($server->{preferred} &&
           $server->{preferred}->{continents} &&
           grep { $_ eq $continent } @{$server->{preferred}->{continents}})
    {
        $match = "preferred";
    }
    elsif ($server->{allowed} &&
           $server->{allowed}->{countries} &&
           grep { $_ eq $country } @{$server->{allowed}->{countries}})
    {
        $match = "allowed";
    }
    elsif ($server->{allowed} &&
           $server->{allowed}->{continents} &&
           grep { $_ eq $continent } @{$server->{allowed}->{continents}})
    {
        $match = "allowed";
    }
    elsif ($server->{denied} &&
        $server->{denied}->{countries} &&
        grep { $_ eq $country } @{$server->{preferred}->{countries}})
    {
        $match = "denied";
    }
    elsif ($server->{denied} &&
           $server->{denied}->{continents} &&
           grep { $_ eq $continent } @{$server->{preferred}->{continents}})
    {
        $match = "denied";
    }
    elsif ($server->{allowed})
    {
        $match = "denied";
    }
    else
    {
        $match = "allowed";
    }

    return $match;
}

#
# Compute the great circle distance between two points
#
sub distance
{
    my $lat1 = deg2rad(shift);
    my $lon1 = deg2rad(shift);
    my $lat2 = deg2rad(shift);
    my $lon2 = deg2rad(shift);

    return great_circle_distance($lon1, pip2 - $lat1, $lon2, pip2 - $lat2);
}
